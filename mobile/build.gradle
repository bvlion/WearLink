plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

android {
    namespace 'net.ambitious.android.httprequesttile'
    compileSdk 33

    defaultConfig {
        applicationId "net.ambitious.android.httprequesttile"
        minSdk 30
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file('./release.keystore')
            storePassword 'releaseStorePassword'
            keyAlias 'releaseKeyAlias'
            keyPassword 'releaseKeyPassword'
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "ADMOB_NATIVE_KEY", "\"ca-app-pub-3940256099942544/2247696110\""
            manifestPlaceholders = [
                    admob_key: "ca-app-pub-3940256099942544~3347511713"
            ]
        }
        release {
            minifyEnabled true
            buildConfigField "String", "ADMOB_NATIVE_KEY", "\"" + System.getenv()['ANDROID_ADMOB_NATIVE_KEY'] + "\""
            manifestPlaceholders = [
                    admob_key: System.getenv()['ANDROID_ADMOB_KEY'] ?: ""
            ]
            signingConfig = signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
        dataBinding true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.6'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

dependencies {
    implementation(project(":shared"))

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'com.google.android.gms:play-services-ads-lite:22.1.0'
    def compose_ui_version = captureVersion(implementation('androidx.compose.ui:ui:1.4.2'))
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.ui:ui-viewbinding:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.4.3'

    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'

    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    def room_version = captureVersion(implementation("androidx.room:room-runtime:2.5.1"))
    annotationProcessor "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    wearApp project(":wear")
}