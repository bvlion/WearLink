plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.github.triplet.play' version '3.8.4'
}

android {
    namespace 'net.ambitious.android.httprequesttile'
    compileSdk project.COMPILE_SDK.toInteger()
    apply from: '../signing.gradle', to: android

    defaultConfig {
        applicationId "net.ambitious.android.httprequesttile"
        minSdk project.MIN_SDK.toInteger()
        targetSdk project.TARGET_SDK.toInteger()
        versionCode 100000000 + project.VERSION_CODE.toInteger()
        versionName project.VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "ADMOB_NATIVE_KEY", "\"ca-app-pub-3940256099942544/2247696110\""
            manifestPlaceholders = [
                    admob_key: "ca-app-pub-3940256099942544~3347511713"
            ]
        }
        release {
            minifyEnabled true
            buildConfigField "String", "ADMOB_NATIVE_KEY", "\"" + System.getenv()['ADMOB_NATIVE_KEY'] + "\""
            manifestPlaceholders = [
                    admob_key: System.getenv()['ADMOB_KEY'] ?: ""
            ]
            signingConfig = signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
        dataBinding true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.6'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

dependencies {
    implementation project(":shared")

    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'com.google.android.gms:play-services-ads-lite:22.2.0'
    def compose_ui_version = captureVersion(implementation('androidx.compose.ui:ui:1.4.3'))
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.ui:ui-viewbinding:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_ui_version"
    implementation 'com.google.android.gms:play-services-wearable:18.0.0'

    implementation 'androidx.compose.material:material:1.4.3'

    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    wearApp project(":wear")
}

play {
    track.set("alpha")
    serviceAccountCredentials.set(file("../google-play-service.json"))
    artifactDir.set(file("../build/app-bundles"))
}